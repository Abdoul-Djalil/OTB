INCLUDE_ONCE_MACRO(MUPARSERX)

SETUP_SUPERBUILD(MUPARSERX)

package_require_cxx11(MUPARSERX)

#Don't build muparserx examples
set(MUPARSERX_FLAGS)
list(APPEND MUPARSERX_FLAGS -DBUILD_EXAMPLES:BOOL=OFF)

if(MSVC AND BUILD_SHARED_LIBS)
 list(APPEND MUPARSERX_FLAGS -DCMAKE_WINDOWS_EXPORT_ALL_SYMBOLS:BOOL=ON)
endif()

# forward compilation flags (muparserx has to be compile with cxx11 and muparserx CMakeLists.txt does not seem to add this flag automatically...)
set(MUPARSERX_SB_COMPILATION_FLAGS
   -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
   -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
   -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS}
   -DCMAKE_MODULE_LINKER_FLAGS:STRING=${CMAKE_MODULE_LINKER_FLAGS}
   -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS}
   -DCMAKE_STATIC_LINKER_FLAGS:STRING=${CMAKE_STATIC_LINKER_FLAGS}
   )
 
ExternalProject_Add(MUPARSERX
  PREFIX MUPARSERX
  URL "https://github.com/beltoforion/muparserx/archive/v4.0.7.zip"
  URL_MD5 67819fc478436ea8f647c804e6f2e1a2
  BINARY_DIR ${MUPARSERX_SB_BUILD_DIR}
  INSTALL_DIR ${SB_INSTALL_PREFIX}
  DOWNLOAD_DIR ${DOWNLOAD_LOCATION}
  CMAKE_CACHE_ARGS
  ${SB_CMAKE_CACHE_ARGS}
  ${MUPARSERX_SB_COMPILATION_FLAGS}
  ${MUPARSERX_FLAGS}
  CMAKE_COMMAND ${SB_CMAKE_COMMAND}
  DEPENDS ${MUPARSERX_DEPENDENCIES}
  )

#Patch mpParserMessageProvider.cpp. This patch is integrated upstream but not yet released (last release is 4.0.7)
SUPERBUILD_PATCH_SOURCE(MUPARSERX)
